plugins {
    id 'fabric-loom' version '1.7-SNAPSHOT' apply false
    id 'maven-publish'
    id 'com.replaymod.preprocess' version 'SNAPSHOT'
    id "com.github.johnrengelman.shadow" version "7.1.2" apply false
}


preprocess {
    def mc116 = createNode("1.16.5", 1_16_05, 'yarn')
    def mc118 = createNode('1.18.2', 1_18_02, 'yarn')
    def mc119 = createNode('1.19.4', 1_19_04, 'yarn')
    def mc1201 = createNode('1.20.1', 1_20_01, 'yarn')
    def mc1204 = createNode("1.20.4", 1_20_04, "yarn")
    def mc1210 = createNode("1.21", 1_21_00, "yarn")

	mc116.link(mc118, file("versions/mapping-1.16-1.18.txt"))
    mc118.link(mc119, file('versions/mapping-1.18-1.19.txt'))
    mc119.link(mc1201, file('versions/mapping-1.19-1.20.txt'))
    mc119.link(mc1204, file('versions/mapping-1.19-1.20.txt'))
    mc1204.link(mc1210, null)
}


//https://github.com/SkyDynamic/UBB-Carpet-Addition/blob/main/build.gradle
tasks.register('buildAll') {
    subprojects {
        dependsOn project.name + ':remapShadowJar'
    }
    doFirst {
        delete fileTree(project.projectDir.toPath().resolve('build/libs')) {
            include '*'
        }
        copy {
            subprojects {
                def libDir = project.projectDir.toPath().resolve('build/libs')
                from(libDir) {
                    include '*.jar'
                    exclude '*-dev.jar', '*-sources.jar'
                }
                into 'build/libs/'
                duplicatesStrategy DuplicatesStrategy.INCLUDE
            }
        }
    }
}
tasks.register('cleanAll') {
    subprojects {
        dependsOn project.name + ':clean'
    }

    doFirst {
        delete "./build"
    }

}

